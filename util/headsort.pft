/* ----------------------------------------------------------------
 *  Clave de ordenacion para los encabezamientos (nombres, temas).
 *
 *  (c) 2003-2004 Fernando J. Gomez - CONICET - INMABB
 * ----------------------------------------------------------------
 *
 *  Testear con: mx testsort pft=v1/,@headsort.pft,/#
 *
 *  ATENCION: Este PFT tambien es usado para ordenar la base de titulos,
 *  y para generar el archivo invertido de dicha base. Quizas convendria
 *  usar un PFT especifico para estos casos (una razon: la necesidad de
 *  usar los indicadores de ordenacion).
 *
 *  TO-DO: cómo ordenar la Ñ?
 *
 *  TO-DO: mejorar el ordenamiento de subject headings. Por ejemplo,
 *  esta lista, que queda ordenada de manera puramente alfabética:
 *
 *  	Celts—Australia—History. 
 *  	Celts—Bibliography. 
 *  	Celts—Bibliography—Catalogs. 
 *  	Celts—Church history. 
 *  	Celts—Congresses. 
 *  	Celts—Domestic animals. 
 *  	Celts—Encyclopedias. 
 *  	Celts—Europe. 
 *
 *  debería reordenarse de manera más lógica tal vez así:
 *
 *  	Celts—Bibliography.            FORM
 *  	Celts—Bibliography—Catalogs. 
 *  	Celts—Congresses.
 *  	Celts—Encyclopedias. 
 *
 *  	Celts—Church history.          TOPIC
 *  	Celts—Domestic animals. 
 *
 *  	Celts—Australia—History.       PLACE
 *  	Celts—Europe. 
 *
 *  Consultar filing rules.
 * ----------------------------------------------------------------
 */


/* ------------------------------------------------
 *  Caracteres auxiliares
 *  v111: flag seteado en h-browse.xis
 * ------------------------------------------------
 */

if p(v111) then
	proc('a29¦ ¦a30¦ ¦a31¦ ¦'),      /* llamado desde h-browse.xis */
else
	proc('a29¦¦a30¦¦a31¦¦'),    /* llamado al generar el OPAC */
fi,


proc('d1001a1001¦',v1,'¦'),


/* ------------------------------------------------
 *  Quitamos la puntuacion final (es necesario?)
 * ------------------------------------------------
 */
 
if '.?!)' : right(v1001,1) then
	proc(
		'd1001a1001¦',
			mid(v1001,1,size(v1001)-1),
		'¦',
	),
fi,


/* -------------------------------------------------------
 *  Usamos los indicadores de ordenacion para los titulos
 * -------------------------------------------------------
 */
 
/* ATENCION: usamos ":" y no "=" antes de 'TITLE' */
if s(mpu,mstname,mpl) : 'TITLE' and v13 <> '0' then
	proc(
		'd1001a1001¦',
			v1001.2,
			mid(v1001*2,val(v13) + 1,size(v1001)),
		'¦',
	)
fi,


/* --------------------------------------------------
 *  Construimos la clave, procesando cada "subcampo"
 * --------------------------------------------------
 */
 
proc(
	'a1000¦',
		replace(v1001*1,'~','¦a1000¦'),
	'¦',
),

/* Loop sobre las ocurrencias de v1000 */
(,
	if 'xyzv' : v1000.1 then
		v30[1],
	else if iocc > 1 /* v1000.1 <> 'a' ? */ then
		v31[1],
	fi,fi,

	replace(replace(replace(replace(
	replace(replace(replace(s(mpu,v1000*1),
		')',''),
		'(',''),
		':',''),
		',',''),
		'-',' '),
		'"',''),
		s("'"n9999),''),  /* quitamos apostrofes */
),

v29,  /* terminador */


/* Eliminamos campos auxiliares */
proc('d29d30d31d1000d1001'),
