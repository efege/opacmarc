/*

Aquí nos ocupamos de dos tareas:

    1. Reasignar los tags a los campos del registro de autoridad.
    2. Ajustar los indicadores en los encabezamientos.

Este es un ejemplo de las conversiones que necesitamos realizar:

REGISTRO DE NOMBRE

    Antes:
    1  «12^aAmaldi, Ugo,^d1875-1957»
    2  «032032»
    3  «700»

    Después:
    001  «032032»
    100  «1#^aAmaldi, Ugo,^d1875-1957»   <= nótese que el 2do indicador queda en '#'


REGISTRO DE TITULO
    
    Antes:
    1  «0#^aAmerican mathematical monthly»
    2  «064560»
    3  «730»
    
    Después:
    001  «064560»
    130  «#0^aAmerican mathematical monthly»   <= nótese que los indicadores están permutados
*/


/* En primer lugar reasignamos los tags, de esta manera: */

'd1d2d3', /* adiós a los tags viejos */

/* El campo 1 (encabezamiento) va a un campo 1xx, donde 'xx' se determina en función del campo 3 (tag de origen).
Si el tag de origen es 440, entonces 'xx' = '30'; en caso contrario, 'xx' coincide con los 2 dígitos finales del tag de origen. */
proc(
    'a1',if v3='440' then '30' else v3*1, fi,'|',
        v1,
    '|'
),
'd100','d110','d111','d130', /* necesitamos borrarlos para poder hacer el ajuste de indicadores más adelante */

/* El campo 2 (ID) va al campo 001 */
'a001|',v2,'|',

/* El campo 3 (tag de origen) lo descartamos, pues ya no se va a necesitar */


/* En segundo lugar ajustamos los indicadores en los encabezamientos. Las tablas que siguen explican la situación
   (para más detalles ver la documentación de MARC 21)
   
   
    ** NOMBRES (PERSONAS, ENTIDADES, REUNIONES) **

    En el formato bibliográfico tenemos:
        tag       ind1    ind2
        -----------------------------------
        100       0,1,3,  #
        700       0,1,3   #,2
        110       0,1,2   #
        710       0,1,2   #,2
        111       0,1,2   #
        711       0,1,2   #,2

    Y en el formato de autoridades:
        tag     ind1    ind2
        ------------------------
        100     0,1,3   #
        110     0,1,2   #
        111     0,1,2   #
        
    Por lo tanto, sólo tenemos que asegurarnos de dejar un '#' en el 2do indicador.

    
    ** TITULOS **
    
    En el formato bibliográfico tenemos:
        tag       ind1    ind2
        -----------------------------------
        130       0-9     #
        730       0-9     #/2 (analítico)
        630       0-9     0-7 (tesauro)
        440       #       0-9
        830       #       0-9
        
    Y en el formato de autoridades:
        tag   ind1    ind2
        -------------------------------
        130   #       0-9
    
    Por lo tanto, tenemos que poner un '#' en el 1er indicador, y (en el caso de títulos que provengan de 130, 730, 630)
    mover el valor del 1er indicador al segundo.
*/

if p(v100) then 
    'a100|',
        v100.1,  /* el 1er indicador se preserva */
        '#',     /* el 2do indicador se deja en '#' */
        v100*2,
    '|',
    
else if p(v110) then 
    'a110|',
        v110.1,  /* el 1er indicador se preserva */
        '#',     /* el 2do indicador se deja en '#' */
        v110*2,
    '|',
    
else if p(v111) then 
    'a111|',
        v111.1,  /* el 1er indicador se preserva */
        '#',     /* el 2do indicador se deja en '#' */
        v111*2,
    '|',
    
else if p(v130) then
    'a130|',
        '#',     /* el 1er indicador se deja en '#' */
        if '440~830' : v3 then
            v130*1.1,    /* el segundo indicador se preserva (440,830) ... */
        else
            v130.1,      /* ... o se toma del primero (130,630,730) */
        fi,
        v130*2,
    '|',
    
fi,fi,fi,fi,
