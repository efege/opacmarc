/* ================================================================== 
 * HEADINGS.PFT
 * Visualización de términos de un índice (autores, temas)
 *
 * FIXME - Usar gizmo & => &amp;
 * ==================================================================
 */


/*
Análisis (a grandes rasgos)

Sobre el tratamiento de las referencias, ver también:
http://catalis.uns.edu.ar/doku/doku.php/notas/referencias_en_el_opac


input:
    - un registro con campos:
        - v1: heading, e.g. "~aBlondel, Jean,~d1929-"
        - v4: heading autorizado; indica que v1 es una referencia de véase
        - v5: 
        - v11: postings (excepto si v1 es una referencia de véase)
        
    - campos "virtuales":
        - v1001: índice en el loop
        - v1501: versión resaltada de v1, e.g. "~aBlondel, <span class='hl'>Jean</span>,~d1929-"

1. construimos el parámetro query para el link:

    v1502 := v4 or v5 or v1 (el primero que aparezca, en ese orden)
    v1502 := urlencode(v1502)

2. generamos el display (fila de la tabla):

    <tr>
    
        <td>
            contador: v1001   (para resultados de búsqueda)
        </td>
    
        <td>
            postings: v11   (vacío si v1 es una referencia de véase)
        </td>
    
        <td>
            if p(v4)   ("see" reference)
                - mostrar v1501
                - mostrar "véase"
                - mostrar link (query: v1502, texto: v4)
                TO-DO: mostrar postings junto al link?
    
            else  (encabezamiento autorizado)
            
                - mostrar link (query: v1502, texto: v1501)
                if p(v5)    ("see also" reference)
                    - mostrar "véase además"
                    - para cada v5:
                        - mostrar link (query: v5, texto: v5)
        </td>
    </tr>
*/


/* Campo 1502: valor del parámetro query. Veamos de cuál campo (v1, v4, v5) lo tomamos:  */
proc('d1502a1502¦',
    if p(v4) then
        v4,             /* "see" reference */
    else if p(v5) then
        v5,             /* "see also" reference */
    else
        v1,             /* default: authorized form */
    fi,fi,
'¦'),


/*  ---------------------------------------------------------------
 *  Codificamos (urlencode) el parámetro query
 *  ---------------------------------------------------------------
 */
proc('d7000a7000¦',replace(v1502,'~','^'),'¦'),
,@URLENCODE.PFT,
proc('d1502a1502¦',v7000,'¦'),


/*  ---------------------------------------------------------------
 *  Mostramos encabezamientos, incluyendo referencias
 *  ---------------------------------------------------------------
 */
'<tr class="headingRow', if right(f(val(v1001)/2,1,1),1) = '0' then ' headingEven' else ' headingOdd' fi, '"',
    /* Resaltamos el "centro del entorno" */
    if p(v2030) and mfn = val(v3009) then
        ' style="background: #D0D0D0;"',
    fi,
'>'/

/*  ---------------------------------------------------------------
 *  contador (no se usa para browsing, sí para resultados)
 *  ---------------------------------------------------------------
 */
if p(v1001) and not v2101 : 'BROWS' then
    '<td align="right" width="40" valign="top" class="counter ',v2003,if v1001 = v2009 or val(v1001) = 1 then ' headingFirstRow' fi'" style="width: 2em;">',
        v1001".",x1,
    '</td>'/
fi,

/*  ---------------------------------------------------------------
 *  checkbox (para selección múltiple)
 *  ---------------------------------------------------------------
 */
/*
'<td valign="top" width="20">'/
    '<input type="checkbox" name="query" value="',replace(v1502,'~','^'),'" size="100" -onclick="changeColor(this)">'/
'</td>'/
*/

/*  ---------------------------------------------------------------
 *  número de postings/hits
 *  ---------------------------------------------------------------
 */
/* ATENCION: no tienen sentido para las "see references" */
/*if p(v11) then*/
    '<td class="lighter ',v2003,if v1001 = v2009 or val(v1001) = 1 then ' headingFirstRow' fi'" style="width: 3em; text-align: right; padding-right: 0;" valign="top">',
        if p(v11) then '[',v11,']', fi,
    '</td>'/
/*fi,*/

/*  ---------------------------------------------------------------
 *  término del índice
 *  ---------------------------------------------------------------
 */
'<td class="',v2003, if v1001 = v2009 or val(v1001) = 1 then ' headingFirstRow' fi,' headingCellIndent">',

/*  ---------------------------------------------------------------
 *  Caso 1: "see" reference
 *  ---------------------------------------------------------------
 */
if p(v4) then
    '<div style="font-weight: bold;">'v1501'</div>',
    '<div style="background: url(/img/right.gif) no-repeat; padding-left: 38px;">',
        'Vea: ',
        '<a href="',
            getenv('SCRIPT_NAME'),
            '?IsisScript=',v2000,
            '&amp;db=',v2104,
            '&amp;task=BIB-H-SEARCH',
            '&amp;index=',v2003,
            '&amp;query=',replace(replace(v1502,'~','^'),'&','%26'),
        '" style="font-weight: bold">',
        proc('d1880a1880¦a',replace(v4,'~','¦a1880¦'),'¦'),
        (,
            if 'xyzv' : v1880.1 then
                '&#8212;',v1880*1,
            else
                x1,v1880*1,  /* FIXME - no debe ir un espacio al comienzo */
            fi,
        ),
        '</a>',
    '</div>',
    
/*  ---------------------------------------------------------------
 *  Caso 2: encabezamiento autorizado
 *  ---------------------------------------------------------------
 */
else
    '<a ', if val(v11) = 0 then 'href="' else 'href="' fi,
        getenv('SCRIPT_NAME'),
        '?IsisScript=',v2000,
        '&amp;db=',v2104,
        '&amp;task=BIB-H-SEARCH',
        '&amp;index=',v2003,
        '&amp;query=',v1502,
    '" style="font-weight: bold;', if p(v2) and '830~440' : v2 /* series */ then 'font-style: italic;' fi, '">',
    
    if v2003 = 'TITLE' then 
        '<span style="font-weight: normal;">',mid(v1501,1,val(v13)+1),'</span>',  /* non-filing chars */
        
        if v1501 : ' / ' then 
            replace(mid(v1501,val(v13)+2,size(v1501)),' / ','</a> / '),
        else
            mid(v1501,val(v13)+2,size(v1501)),
            '</a>',
        fi,
        
        if right(v1501,1) <> '.' then '.' fi,
        
        if '830~440' : v2 then ' (Serie)', fi, /* aclaramos qué títulos corresponden a series */
        
        /* omitimos este tramo, por eso el 1=0 */
        if 1 = 0 then
            proc('d111a111¦',replace(v1501,' ','¦a111¦'),'¦'),
            if nocc(v111) > 25 then  /* cantidad de palabras */
                (,
                    if iocc <= 25 then
                        v111,x1,
                    else
                        break,
                    fi,
                ),
                '.&nbsp;.&nbsp;.',
            else
                v1501,
                if right(v1501,1) <> '.' then '.' fi,
            fi,
        fi,
        
    else          /* v2003 != 'TITLE' */
        v1501,
        '</a>',
    fi,
    
    
    /*  ---------------------------------------------------------------
     *  Caso 2a: see also references
     *  ---------------------------------------------------------------
     */
    if p(v5) then
        /*'<div style="font-weight: bold;">'v1501'</div>',*/
        '<div style="background: url(/img/right.gif) no-repeat; padding-left: 38px;">',
            'Vea también: ',
            (
                '<a href="',
                    getenv('SCRIPT_NAME'),
                    '?IsisScript=',v2000[1],
                    '&amp;db=',v2104[1],
                    '&amp;task=BIB-H-SEARCH',
                    '&amp;index=',v2003[1],
                    '&amp;query=',replace(replace(v5,'~','^'),'&','%26'),
                '" style="font-weight: bold">',
                    v5,
                '</a>',
                '<br>',
            )
        '</div>',
   fi, 
fi,

'</td>'/
'</tr>'/
